title: Media
new-page: true
is-card: true
tags: party_affairs
view-if: media_timer <= 0
on-arrival: month_actions += 1; media_timer += 6;
card-image: img/sangerbund.jpg
#subtitle: Setting the direction for our newspapers and radio.

= Media

The <span style="color: #c00000;">**SPD**</span> runs a veritable media empire, with many newspapers throughout every German city and region. We could use the media for various purposes...

- @commercial
- @campaign
- @ideological
- @tv
- @tv_2
- @tv_3
- @tv_4
- @return: We should not do anything with media at the moment.
- @easy_discard
- @cancel_advisor_action

@commercial
title: Make our newspapers more commercialized by broadening their appeal beyond party supporters.
subtitle: This might upset ideological purists, but it may bring in more funds and perhaps expose the middle class to socialist views.
on-arrival: resources += 1; commercialized_media += 1; new_middle_spd += 4*(1 - dissent); old_middle_spd += 3*(1-dissent); left_dissent += 10; labor_dissent += 10

Our commercialized newspapers are able to bring in more funds. However, the left and labor factions express their displeasure.

@campaign
title: The purpose of our media is to elect more Social Democrats.
subtitle: Our newspapers will focus on the electoral campaign.
on-arrival: campaign_media += 1; workers_spd += 4*(1 - dissent); unemployed_spd += 2*(1-dissent); left_mobilization += 0.01

Our campaign media is mostly reaching the workers who are already saturated with Socialist propaganda, but it must be doing *something*, or at least that is what we hope.

@ideological
title: We will have vibrant ideological debate within our newspapers.
subtitle: There will be space for all factions to air their views.
on-arrival: left_dissent -= 6; labor_dissent -= 6; green_dissent -= 6; reformist_dissent -= 6

This has been our media course historically, and it has successfully paved over the many tensions in our party.

@tv
title: Why not fund a TV station?
subtitle: -2 resources
choose-if: resources >= 2
unavailable-subtitle: Not enough resources.
view-if: tv = 0
on-arrival: tv += 1; resources -= 2; left_mobilization += 0.01; new_middle_spd += 4*(1 - dissent); old_middle_spd += 3*(1 - dissent); workers_spd += 2*(1 - dissent); labor_dissent += 5; socialism += 3*(1 - dissent); nationalism -= 3*(1 - dissent) if pacifism > 1; pro_eu += 3*(1-dissent) 

Our new TV station allows us to connect to people in a more intimate setting than before, spreading the message of socialism along with music and other popular content. Since most proletarians cannot afford family TVs right now, we are reaching the middle class, a fact of which our labor comrades are painfully aware.

@tv_2
title: We must keep on building up our TV network.
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
view-if: tv > 0 and tv <= 3
on-arrival: tv += 1; resources -= 1; left_mobilization += 0.01; new_middle_spd += 5*(1 - dissent); old_middle_spd += 3*(1 - dissent); workers_spd += 3*(1 - dissent); socialism += 3*(1 - dissent); nationalism -= 3*(1 - dissent)*(pacifism-2) if pacifism > 2; pro_eu += 2*(1-dissent)

Our new TV network has a mix of popular and political programming, and is proving a success.

@tv_3
title: We must keep on building up our TV network.
subtitle: It is now financially self-sufficient, and does not require any more resources.
view-if: tv > 3 and tv <= 5 and year <= 1931
on-arrival: tv += 1; left_mobilization += 0.01; new_middle_spd += 5*(1 - dissent); old_middle_spd += 4*(1 - dissent); workers_spd += 4*(1 - dissent); rural_spd += 3*(1 - dissent); unemployed_spd += 2*(1-dissent); socialism += 3*(1 - dissent); nationalism -= 3*(1 - dissent)*(pacifism-2) if pacifism > 2; pro_republic += 3*(1-dissent)*(democratization-2) if democratization > 2

Our TV network has become a successful tool for spreading our message.

@tv_4
title: We should keep on supporting our radio network.
subtitle: As radio becomes more common, the novelty is beginning to wear off, and radio is becoming saturated in society. -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
view-if: tv > 3 and (tv >= 6 or year >= 1932) and tv <= 9
on-arrival: tv += 1; resources -= 1; left_mobilization += 0.01; new_middle_spd += 3*(1 - dissent); old_middle_spd += 2*(1 - dissent); workers_spd += 3*(1 - dissent); rural_spd += 2*(1 - dissent); unemployed_spd += 2*(1-dissent); socialism += 2*(1 - dissent); nationalism -= 2*(1 - dissent)*(pacifism-2) if pacifism > 2; pro_eu += 2*(1-dissent)

We are continuing to invest in our TV station. Both commercial and political channels are becoming more prevalent, making it harder to spread our message amidst the competition.
