title: Labor Affairs
new-page: true
is-card: true
tags: govt_affairs, cabinet
on-arrival: labor_affairs_timer = 6; month_actions += 1; labor_affairs_seen = 1
view-if: spd_in_government = 1 and labor_minister_party = "SPD" and labor_affairs_timer = 0 and strike_term_seen = 0
card-image: img/portraits/WissellRudolf.jpg
#subtitle: Addressing labor unrest and strikes.

= Labor Affairs

The industrial Ruhr region is aflame with labor unrest. The employers have started a major lockout to force the workers to take more cuts to wages and benefits.  There is a need for the Labor Ministry to arbitrate in conflicts between employers and workers.

- @support_labor: Support labor in their demands.
- @support_employers: Support the employers in their demands.
- @balance: Try to strike a compromise between the sides.
- @easy_discard
#- @cancel_advisor_action

@support_labor
subtitle: Popular among the workers, this will alienate the bourgeoisie.
on-arrival: labor_strength += 5; labor_dissent -= 5; left_dissent -= 5; workers_spd += 6*(1-dissent); lspd_dissent -= 1 if lspd_dissent > 0; lspd_left += 1; rdp_dissent += 1; rdp_relation -= 4; rdp_left -= 1; capital_strike_progress += 1 if rdp_relation <= 30; capital_strike_progress += 1 if unemployed > 15; capital_strike_progress += 1 if unemployed > 24; labor_goal_completed += 1; inflation += 0.1; pro_labor += 1; strike_term_seen += 1; economic_growth -= 0.1; labor_goal_spd += 1; goal_spd_cancel_peoples += 1;

Supporting labor is always popular in the SPD, although many of our coalition partners represent employers, and they are less enthused.

@support_employers
subtitle: Popular among the bourgeoisie, much less so among the workers.
on-arrival: labor_dissent += 8; workers_spd -= 3; lspd_left -= 1; rdp_dissent -= 1 if rdp_dissent > 0; rdp_left += 1; rdp_relation += 5; cvp_relation += 5; workers_lspd += 3 if not lspd_in_government; lspd_dissent += 1; kpd_relation -= 8; capital_strike_progress -= 1 if capital_strike_progress > 1; capital_strike_progress -= 1 if capital_strike_progress > 8; pro_labor -= 1; strike_term_seen += 1; goal_spd_cancel += 1; labor_goal_spd_peoples += 1
The more radical workers despise our suppression of their strikes, recalling the days of Friedrich Ebert.

@balance
subtitle: Both sides need to share the pain in the current crisis.
on-arrival: labor_dissent += 4; workers_spd -= 2; lspd_left -= 0.5; lspd_relation -= 5; strike_term_seen += 1; labor_goal_spd_peoples += 1

Many workers are unhappy at our compromises, but they will learn to accept that we are all in a shared struggle against economic forces. 



# options: support the workers in their demands, pushing back against the employers.
# this is the approach favored by the unions. While they do not get everything they desire, an agreement favorable to the workers is reached. The employer associations and their bourgeois allies accuse us of overstepping our bounds.
# try to make some compromise between the sides. Both sides need to share the pain in the current crisis.
# whatever it takes, end the strikes and unrest as soon as possible.
# this is not our business to intervene.
# We are the party of labor, and it is not our business to intervene? There is incredulity in the ranks. The workers are less likely to trust us in the future.
